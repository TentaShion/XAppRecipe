plugins {
  id 'com.android.application'

  // Kotlin
  id 'kotlin-android'
  id 'kotlin-android-extensions'
  id 'kotlin-kapt'

  // AndroidX Navigation
  id "androidx.navigation.safeargs.kotlin"
}

android {
  buildFeatures {
    dataBinding = true
  }
  buildToolsVersion rootProject.ext.build_tools_version
  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  compileSdkVersion compile_sdk_version
  defaultConfig {
    applicationId "work.shion.xapprecipe"
    minSdkVersion min_sdk_version
    targetSdkVersion compile_sdk_version
    versionCode appver_code
    versionName appver_name
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

repositories {
  flatDir {
    dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
  }
}

dependencies {
  implementation project(':capacitor-android')
  implementation project(':capacitor-cordova-android-plugins')

  // Java 8+
  coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${ver_desugar_jdk}"

  // Local Project
  implementation project(":app_core")
  implementation project(":app_data")


  // AndroidX
  implementation "androidx.constraintlayout:constraintlayout:${ver_droidx_constraint}"
  implementation "androidx.core:core-ktx:${ver_droidx_core}"
  implementation "androidx.fragment:fragment-ktx:${ver_droidx_fragment}"
  implementation "androidx.navigation:navigation-fragment-ktx:${ver_droidx_navigation}"
  implementation "androidx.navigation:navigation-ui-ktx:${ver_droidx_navigation}"

  // AndroidPreparation
  debugImplementation "work.shion.androidpreparation:debugger:${ver_prep_debugger}"
  implementation "work.shion.androidpreparation:views:${ver_prep_views}"

  // Kotlin
  implementation platform("org.jetbrains.kotlin:kotlin-bom:${ver_kotlin}")
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

  // Kotlin Coroutines
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${ver_kotlin_coroutines}"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${ver_kotlin_coroutines}"

  // Material Design
  implementation "com.google.android.material:material:${ver_material}"
}

apply from: 'capacitor.build.gradle'

try {
  def servicesJSON = file('google-services.json')
  if (servicesJSON.text) {
    apply plugin: 'com.google.gms.google-services'
  }
} catch (Exception e) {
  logger.warn("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
